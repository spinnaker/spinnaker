apply plugin: 'spinnaker.application'
apply from: "$rootDir/gradle/ldap.gradle"

ext {
  springConfigLocation = System.getProperty('spring.config.location', "${System.getProperty('user.home')}/.spinnaker/")
  springProfiles = System.getProperty('spring.profiles.active', "test,local")
}

run {
  systemProperty('spring.config.location', project.springConfigLocation)
  systemProperty('spring.profiles.active', project.springProfiles)
}
mainClassName='com.netflix.spinnaker.gate.Main'

repositories {
  maven {
    url 'https://repo.spring.io/libs-snapshot'
  }
}

dependencies {
  compile project(":gate-core")
  compile project(":gate-proxy")

  spinnaker.group('retrofitDefault')
  spinnaker.group("test")
  spinnaker.group("fiat")
  compile spinnaker.dependency("kork")
  compile spinnaker.dependency("korkStackdriver")
  compile spinnaker.dependency("korkSwagger")
  compile spinnaker.dependency("korkWeb")
  compile spinnaker.dependency("frigga")
  compile spinnaker.dependency("logstashEncoder")
  compile spinnaker.dependency('cglib')
  compile spinnaker.dependency('jedis')

  compile('org.apache.commons:commons-io:1.3.2')
  compile('org.springframework.session:spring-session-data-redis:1.1.1.RELEASE')
  compile "org.springframework.data:spring-data-redis:1.8.10.RELEASE"
  compile "org.springframework.session:spring-session:1.3.1.RELEASE"

  // OAuth2
  compile 'org.springframework.security.oauth:spring-security-oauth2:2.1.1.RELEASE'
  compile 'com.squareup.retrofit:converter-simplexml:1.9.0' // Github OAuth

  // LDAP
  compile("org.springframework.security:spring-security-ldap:${spinnaker.version('springSecurity')}")

  // SAML
  compile('org.opensaml:opensaml:2.6.4')
  compile("org.springframework.security.extensions:spring-security-saml2-core:1.0.2.RELEASE")
  compile("org.springframework.security.extensions:spring-security-saml-dsl:1.0.0.M3") {
    exclude group: 'org.springframework.boot'
    exclude group: 'org.springframework.security'
  }

  // IAP
  compile('com.nimbusds:nimbus-jose-jwt:5.2')

  compileOnly spinnaker.dependency("lombok")

  testCompile "com.squareup.okhttp:mockwebserver:${spinnaker.version('okHttp')}"
  testCompile apachedsDependencies
  testCompile spinnaker.dependency("korkJedisTest")

  //this brings in the jetty GzipFilter which boot will autoconfigure
  runtime 'org.eclipse.jetty:jetty-servlets:9.2.11.v20150529'
}

configurations.all {
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: "org.slf4j", module: "slf4j-log4j12"
}
