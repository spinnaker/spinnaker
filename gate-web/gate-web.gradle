apply plugin: 'spinnaker.application'

ext {
  springConfigLocation = System.getProperty('spring.config.additional-location', "${System.getProperty('user.home')}/.spinnaker/")
  springProfiles = System.getProperty('spring.profiles.active', "test,local")
}

run {
  systemProperty('spring.config.additional-location', project.springConfigLocation)
  systemProperty('spring.profiles.active', project.springProfiles)
}
mainClassName = 'com.netflix.spinnaker.gate.Main'

repositories {
  maven {
    url 'https://repo.spring.io/libs-snapshot'
  }
}

dependencies {
  implementation project(":gate-core")
  implementation project(":gate-proxy")
  implementation project(":gate-deck-plugins")
  implementation project(":gate-integrations-gremlin")

  implementation "com.squareup.retrofit:retrofit"
  implementation "com.squareup.retrofit:converter-jackson"
  implementation "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml"

  implementation "com.squareup.okhttp:okhttp"
  implementation "com.squareup.okhttp:okhttp-urlconnection"
  implementation "com.squareup.okhttp:okhttp-apache"

  implementation "com.netflix.spinnaker.fiat:fiat-core:$fiatVersion"
  implementation "com.netflix.spinnaker.fiat:fiat-api:$fiatVersion"

  implementation "com.netflix.spinnaker.kork:kork-core"
  implementation "com.netflix.spinnaker.kork:kork-plugins"
  implementation "com.netflix.spinnaker.kork:kork-secrets-aws"
  implementation "com.netflix.spinnaker.kork:kork-secrets-gcp"
  implementation "com.netflix.spinnaker.kork:kork-stackdriver"
  implementation "com.netflix.spinnaker.kork:kork-swagger"
  implementation "com.netflix.spinnaker.kork:kork-web"
  implementation "com.netflix.frigga:frigga"
  implementation "redis.clients:jedis"
  implementation "com.netflix.hystrix:hystrix-core"

  implementation 'commons-io:commons-io'
  implementation 'org.springframework.session:spring-session-data-redis'
  implementation "de.huxhorn.sulky:de.huxhorn.sulky.ulid"
  implementation "com.jakewharton.retrofit:retrofit1-okhttp3-client:1.1.0"

  implementation "org.springframework:spring-web"

  testImplementation project(":gate-ldap") // TODO: Move system tests to own module
  testImplementation project(":gate-basic")
  testImplementation "com.squareup.okhttp:mockwebserver"

  testImplementation "com.squareup.retrofit:retrofit-mock"
  testImplementation "org.springframework.security:spring-security-test"
  testImplementation "org.springframework.security:spring-security-ldap"
  testImplementation "org.springframework.security:spring-security-oauth2-jose"
  testImplementation "com.unboundid:unboundid-ldapsdk"
  testImplementation "com.netflix.spinnaker.kork:kork-jedis-test"

  // Add each included authz provider as a runtime dependency
  gradle.includedProviderProjects.each {
    runtime project(it)
  }
}

configurations.all {
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: "org.slf4j", module: "slf4j-log4j12"
}
