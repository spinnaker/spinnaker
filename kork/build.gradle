/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'io.spinnaker.project' apply false
  alias(libs.plugins.kotlin.jvm) apply false
  id "io.gitlab.arturbosch.detekt" version "1.17.1" apply false
  alias(libs.plugins.kotlin.allopen) apply false
}

allprojects {
  apply plugin: "io.spinnaker.project"
}

subprojects {
  group = "io.spinnaker.kork"

  // This is required for some SpEL expressions to evaluate properly with java
  // 17.  It works with java 11 as well, but isn't required there.
  tasks.withType(Test).configureEach {
    jvmArgs += '--add-opens=java.base/java.util=ALL-UNNAMED'
  }

  // Avoid warnings like
  //
  // Using deprecated '-debug' fallback for parameter name resolution. Compile
  // the affected code with '-parameters' instead or avoid its introspection:
  // com.netflix.spinnaker.config.DefaultServiceClientProvider
  //
  // FWIW, the spring boot gradle plugin enables this since 2.0 via
  // https://github.com/spring-projects/spring-boot/pull/9839.
  tasks.withType(JavaCompile) {
    options.compilerArgs << "-parameters"
  }

  if (it.name != "kork-bom" && it.name != "spinnaker-dependencies") {
    apply plugin: 'java-library'
    test {
      useJUnitPlatform()
    }
    dependencies {
      annotationProcessor(platform(project(":spinnaker-dependencies")))
      annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
      testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }
  }
}
