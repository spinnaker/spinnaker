test {
  useJUnitPlatform()
}

class DownloadTask extends DefaultTask {
  @Input
  String sourceUrl

  @OutputFile
  File target

  @TaskAction
  void download() {
    ant.get(src: sourceUrl, dest: target)
  }
}

final File sdkDownloadLocation = project.file('build/sdkdownload')
final File sdkLocation = project.file('build/oci-java-sdk')

// Oracle BMCS SDK isn't published to any maven repo (yet!), so we manually download, unpack and add to compile/runtime deps
// https://github.com/oracle/oci-java-sdk/issues/25
task fetchSdk(type: DownloadTask) {
  sourceUrl = 'https://github.com/oracle/oci-java-sdk/releases/download/v1.3.2/oci-java-sdk.zip'
  target = sdkDownloadLocation
}

task unpackSdk(type: Sync) {
  dependsOn('fetchSdk')
  from zipTree(tasks.fetchSdk.target)
  into sdkLocation
  include "**/*.jar"
  exclude "**/*-sources.jar"
  exclude "**/*-javadoc.jar"
  exclude "apidocs/**"
  exclude "examples/**"

  // Scary but works. I think clouddriver deps in general need cleaning at some point
  // Even without the oracle bmc sdk 3rd party deps there's still multiple javax.inject and commons-X JARs
  exclude "**/*jackson*.jar"
  exclude "**/*jersey*.jar"
  exclude "**/hk2*.jar"
  exclude "**/*guava*.jar"
  exclude "**/commons*.jar"
  exclude "**/aopalliance*.jar"
  exclude "**/javassist*.jar"
  exclude "**/slf*.jar"
  exclude "**/osgi*.jar"
  exclude "**/validation*.jar"
  exclude "**/jsr305*.jar"
  exclude "**/json-smart*.jar"
  exclude "**/oci-java-sdk-full-shaded-*.jar"
}

task cleanSdk(type: Delete) {
  delete sdkLocation, sdkDownloadLocation
}

tasks.clean.dependsOn('cleanSdk')
tasks.compileJava.dependsOn('unpackSdk')

dependencies {
  compile project(":clouddriver-core")

  compile spinnaker.dependency("frigga")
  compile spinnaker.dependency("bootActuator")
  compile spinnaker.dependency("bootWeb")
  compile spinnaker.dependency("commonsLang")
  compile spinnaker.dependency("korkArtifacts")
  compile spinnaker.dependency("korkExceptions")
  compile spinnaker.dependency("lombok")
  compile spinnaker.dependency("okHttp")

  compile spinnaker.dependency("googleStorage")
  compile spinnaker.dependency("awsS3")
  compile "org.apache.commons:commons-compress:1.14"
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.7"
  compile 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.9.7'
  compile "org.apache.ivy:ivy:2.4.0"

  compile fileTree(sdkLocation)

  testCompile('org.junit.jupiter:junit-jupiter-api:5.2.0')
  testRuntime('org.junit.jupiter:junit-jupiter-engine:5.2.0')
  testCompile 'org.assertj:assertj-core:3.8.0'
  testCompile 'org.junit-pioneer:junit-pioneer:latest.release'

  testCompile 'ru.lanwen.wiremock:wiremock-junit5:1.2.0'
  testCompile 'com.github.tomakehurst:wiremock:latest.release'
}
