/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

ext {
  springConfigLocation = System.getProperty('spring.config.location', "${System.getProperty('user.home')}/.spinnaker/")
}

tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
  systemProperty('spring.config.location', project.springConfigLocation)
}

apply plugin: 'spring-boot'
apply plugin: 'nebula.ospackage'

configurations.all {
  exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

sourceSets {
  main {
    resources {
      srcDir 'src/main/resources'
      srcDir 'config'
    }
  }
}

jar {
  doFirst {
    exclude "${rootProject.name}.yml"
  }
}

dependencies {
  compile project(":front50-core")
  compile project(":front50-cassandra")
  compile project(":front50-gcs")
  compile project(":front50-redis")
  compile project(":front50-s3")
  compile project(":front50-pipelines")
  compile project(":front50-migrations")

  spinnaker.group "bootWeb"

// Replace below with this line when fiat becomes stable.
//  spinnaker.group "fiat"
  compile "com.netflix.spinnaker.fiat:fiat-api:0.3.0"
  compile "org.springframework.boot:spring-boot-starter-actuator:1.2.8.RELEASE"
  compile "org.springframework.boot:spring-boot-starter-web:1.2.8.RELEASE"
  compile "org.springframework.boot:spring-boot-starter-data-rest:1.2.8.RELEASE"
  compile "org.springframework.security:spring-security-config:3.2.9.RELEASE"
  compile "org.springframework.security:spring-security-core:3.2.9.RELEASE"
  compile "org.springframework.security:spring-security-web:3.2.9.RELEASE"

  compile spinnaker.dependency("korkWeb")
  compile spinnaker.dependency("korkSwagger")
  compile spinnaker.dependency("kork")

  testCompile project(":front50-test")
}

applicationName = "front50"
applicationDefaultJvmArgs = ["-Djava.security.egd=file:/dev/./urandom"]
applicationDistribution.from(project.file('config')) {
  into 'config'
}

startScripts {
  defaultJvmOpts = applicationDefaultJvmArgs + ["-Dspring.config.location=/opt/spinnaker/config/"]
  doLast {
    unixScript.text = unixScript.text.replace('DEFAULT_JVM_OPTS=', '''\
        if [ -f /etc/default/spinnaker ]; then
          set -a
          . /etc/default/spinnaker
          set +a
        fi
        DEFAULT_JVM_OPTS='''.stripIndent())
    unixScript.text = unixScript.text.replace('CLASSPATH=$APP_HOME', 'CLASSPATH=$APP_HOME/config:$APP_HOME')
    windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'set CLASSPATH=%APP_HOME%\\config;')
  }
}


String toVers(String v) {
  int idx = v.indexOf('-')
  if (idx != -1) {
    return v.substring(0, idx)
  }
  return v
}
ospackage {
  packageName = "spinnaker-" + project.applicationName
  version = toVers(project.version.toString())
  release '3'
  os = LINUX

  // installer scripts
  postInstall = file('pkg_scripts/postInstall.sh')
  postUninstall = file('pkg_scripts/postUninstall.sh')

  from "${project.buildDir}/install/${project.applicationName}"
    into "/opt/${project.applicationName}"

  from(file('etc/init/front50.conf')) {
    into('/etc/init')
    user = 'root'
    permissionGroup = 'root'
    fileType = CONFIG | NOREPLACE
  }

  from(file('etc/logrotate.d/front50')) {
    into('/etc/logrotate.d')
    user = 'root'
    permissionGroup = 'root'
    fileMode = 0644
    fileType = CONFIG | NOREPLACE
  }

}

buildDeb {
  dependsOn installDist
}

bootRun {
  // jamm is required when using prepared statements with in-memory C*
  def jamm = project.configurations.compile.find { it.name.startsWith('jamm') }
  jvmArgs applicationDefaultJvmArgs + "-javaagent:${jamm}"
}
