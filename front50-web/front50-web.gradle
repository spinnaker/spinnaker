/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'spinnaker.application'

if (gradle.includedProviderProjects.contains(':front50-oracle')) {
  tasks.startScripts.dependsOn(':front50-oracle:unpackSdk')
  tasks.startScripts.mustRunAfter(':front50-oracle:unpackSdk')
}

ext {
  springConfigLocation = System.getProperty('spring.config.location', "${System.getProperty('user.home')}/.spinnaker/")
}

run {
  systemProperty('spring.config.location', project.springConfigLocation)
}
mainClassName = 'com.netflix.spinnaker.front50.Main'

configurations.all {
  exclude group: 'org.slf4j', module: 'slf4j-log4j12'
}

dependencies {
  compile project(":front50-core")
  compile project(":front50-migrations")

  spinnaker.group "bootWeb"

  compile spinnaker.dependency("korkWeb")
  compile spinnaker.dependency("korkStackdriver")
  compile spinnaker.dependency("korkSwagger")
  compile spinnaker.dependency("kork")

  compile "com.graphql-java:graphql-spring-boot-starter:3.9.2"

  testCompile project(":front50-test")

  // Add each included cloud provider project as a runtime dependency
  gradle.includedProviderProjects.each {
    runtime project(it)
  }
}

test {
  environment "AWS_ACCESS_KEY_ID", "null"
  environment "AWS_SECRET_KEY", "null"
}
