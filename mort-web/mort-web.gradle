buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    classpath "com.netflix.nebula:gradle-ospackage-plugin:1.12.2"
  }
}

apply plugin: 'os-package'
apply plugin: 'spring-boot'

configurations.all {
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: "org.slf4j", module: "slf4j-log4j12"
}

ideaConfig {
  appClassName = 'com.netflix.spinnaker.mort.Main'
}

dependencies {
  compile project(":mort-core")
  compile project(":mort-aws")
  spinnaker.group('bootWeb')
  runtime spinnaker.dependency("kork")
}

applicationName = 'mort'
applicationDistribution.from(project.file('config')) {
  into 'config'
}

sourceSets {
  main {
    resources {
      srcDir 'src/main/resources'
      srcDir 'config'
    }
  }
}

jar {
  doFirst { 
    exclude "${rootProject.name}.yml"
  }
}

startScripts {
  doLast {
    unixScript.text = unixScript.text.replace('CLASSPATH=$APP_HOME', 'CLASSPATH=$APP_HOME/config:$APP_HOME')
    windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'set CLASSPATH=%APP_HOME%\\config;')
  }
}

ospackage {
  packageName = project.applicationName
  version = project.version.replaceAll("-SNAPSHOT", "")
  release '3'
  into "/opt/${project.applicationName}"
  from "${project.buildDir}/install/${project.applicationName}"
}

buildDeb {
  dependsOn installApp
  requires("openjdk-7-jre")
}

