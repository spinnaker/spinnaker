import com.github.benmanes.gradle.versions.updates.DependencyUpdatesTask

plugins {
  alias(libs.plugins.kotlin.jvm) apply false
  alias(libs.plugins.kotlin.allopen) apply false
  id("com.adarshr.test-logger") version "2.1.0" apply false
  id("com.github.ben-manes.versions") version "0.28.0" apply false
  id("com.netflix.dgs.codegen") version "4.3.4" apply false
  id "com.diffplug.spotless" version "5.10.2" apply false
  id 'io.spinnaker.project' apply false
}

allprojects {
  group = "io.spinnaker.keel"
  apply plugin: 'io.spinnaker.project'

  if (name != "keel-bom") {
    apply(plugin: "java")
    apply(plugin: "java-library")
    apply(from: "$rootDir/../kotlin.gradle")
    apply(from: "$rootDir/gradle/test.gradle")
    apply(plugin: "com.github.ben-manes.versions")
    apply(plugin: "com.diffplug.spotless")


    dependencies {
      annotationProcessor(platform("io.spinnaker.kork:kork-bom"))
      annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

      implementation(enforcedPlatform("io.spinnaker.kork:kork-bom"))
      implementation("org.slf4j:slf4j-api")

      testImplementation("org.junit.platform:junit-platform-runner")
      testImplementation("org.junit.jupiter:junit-jupiter-api")
      testImplementation("org.junit.jupiter:junit-jupiter-params")
      testImplementation("io.mockk:mockk")
      testImplementation("org.jacoco:org.jacoco.ant:0.8.5")

      testRuntimeOnly("org.junit.platform:junit-platform-launcher")
      testRuntimeOnly("org.junit.jupiter:junit-jupiter-engine")
    }
  }

  configurations.all {
    exclude(group: "javax.servlet", module: "servlet-api")
    exclude(group: "javax.servlet", module: "javax.servlet-api")
    exclude(group: "org.slf4j", module: "slf4j-log4j12")
    exclude(group: "org.slf4j", module: "slf4j-simple")

    resolutionStrategy {
      force(
        "com.squareup.okhttp3:okhttp:$okHttpVersion",
        "com.squareup.okhttp3:okhttp-urlconnection:$okHttpVersion",
        "com.squareup.okhttp3:okhttp-sse:$okHttpVersion",
        "com.squareup.okhttp3:mockwebserver:$okHttpVersion",
        "com.squareup.okhttp3:logging-interceptor:$okHttpVersion",
        "io.github.resilience4j:resilience4j-kotlin:$resilience4jVersion",
        "io.github.resilience4j:resilience4j-retry:$resilience4jVersion",
        "de.huxhorn.sulky:de.huxhorn.sulky.ulid:8.2.0",
        "io.github.microutils:kotlin-logging:1.4.4",
        // needed due to incompatibility with dgs libs
        "com.fasterxml.jackson.module:jackson-module-kotlin:2.11.1"
      )
    }
  }
}

tasks.withType(DependencyUpdatesTask) {
  revision = "release"
  checkConstraints = true
  gradleReleaseChannel = "current"
  rejectVersionIf {
    candidate.version.contains(Regex("-(M|eap|rc|alpha|beta)-?[\\d-]+\$"))
  }
}

defaultTasks(":keel-web:run")
