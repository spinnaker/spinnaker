```groovy
@GrabResolver(name='spinnaker', root='http://dl.bintray.com/spinnaker/spinnaker')
@Grab('com.netflix.spinnaker.client:spinnaker-client:0.2')
@Grab('io.reactivex:rxjava:1.0.0-rc.10')

import com.netflix.spinnaker.client.Spinnaker
import com.netflix.spinnaker.client.model.ops.*
import rx.Observable

class RxDeploy {
  final def spinnaker
  def app, account, region, subnet

  RxDeploy(String gateUrl) {
    spinnaker = Spinnaker.using(gateUrl)
  }

  static RxDeploy using(String gateUrl) {
    new RxDeploy(gateUrl)
  }

  def deploy(String app) {
    this.app = app
    this
  }

  def to(String account) {
    this.account = account
    this
  }

  def into(String region) {
    this.region = region
    this
  }

  def forSubnet(String subnet) {
    this.subnet = subnet
    this
  }

  private Observable<Map<String, List<String>>> now() {
    def ops = spinnaker.application(app).operations()
    rx.Observable.just(ops.bake()).flatMap {
      rx.Observable.from(it.submit().toFuture())
    }.flatMap {
      rx.Observable.from(ops.deploy(it.operation.ami)
                            .withAccount(account)
                            .withRegion(region)
                            .withSubnetType(subnet)
                            .submit()
                            .toFuture())
    }.map {
      it.operation.serverGroups
    }
  }

  def propertyMissing(String prop) {
    if (prop == "now") {
      return now()
    } else {
      return this
    }
  }
}

RxDeploy.using "http://localhost:8989" deploy "mimirdemo" to "test" into "us-east-1" forSubnet "internal" now
```
