apply plugin: 'spinnaker.application'

ext {
  springConfigLocation = System.getProperty('spring.config.location', "${System.getProperty('user.home')}/.spinnaker/")
  springProfiles = System.getProperty('spring.profiles.active', "test,local")
}

run {
  systemProperty('spring.config.location', project.springConfigLocation)
  systemProperty('spring.profiles.active', project.springProfiles)
}
mainClassName = 'com.netflix.spinnaker.fiat.Main'

configurations.all {
  exclude group: 'javax.servlet', module: 'servlet-api'
  exclude group: "org.slf4j", module: "slf4j-log4j12"
}

dependencies {
  spinnaker.group('retrofitDefault')

  compileOnly spinnaker.dependency("lombok")
  annotationProcessor spinnaker.dependency("lombok")

  compile spinnaker.dependency('bootActuator')
  compile spinnaker.dependency('bootWeb')
  compile spinnaker.dependency('kork')
  compile spinnaker.dependency('korkWeb')
  compile spinnaker.dependency("korkStackdriver")
  compile spinnaker.dependency("korkSwagger")

  compile project(':fiat-core')
  compile project(':fiat-api')
  compile project(':fiat-roles')

  // Add each included authz provider as a runtime dependency
  gradle.includedProviderProjects.each {
    runtime project(it)
  }

  testCompile spinnaker.dependency('korkJedisTest')
  testCompile "org.skyscreamer:jsonassert:1.3.0"
}

applicationName = 'fiat'
