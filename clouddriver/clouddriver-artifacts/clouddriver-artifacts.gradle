plugins {
  id 'com.adarshr.test-logger' version '2.1.0' apply false
}
allprojects {
  apply plugin: 'com.adarshr.test-logger'
  tasks.compileGroovy.enabled = false
  sourceSets {
    main {
      java.srcDirs = ['src/main/java']
    }
    integration {
      java.srcDirs = ["src/integration/java"]
      resources.srcDirs = ["src/integration/resources"]
    }
  }

  configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
  }

  dependencies {
    implementation project(":clouddriver-core")
    implementation "io.spinnaker.kork:kork-artifacts"
    implementation "io.spinnaker.kork:kork-credentials"
    implementation "org.springframework.boot:spring-boot-starter-web"

    testImplementation "com.github.tomakehurst:wiremock-jre8-standalone"
    testImplementation "org.assertj:assertj-core"
    testImplementation "org.junit-pioneer:junit-pioneer:0.3.0"
    testImplementation "org.junit.jupiter:junit-jupiter-api"
    testImplementation "org.junit.jupiter:junit-jupiter-params"
    testImplementation "org.springframework.boot:spring-boot-test"
    testImplementation "ru.lanwen.wiremock:wiremock-junit5:1.2.0"
  }
}

subprojects {
  if (file("${projectDir}/src/integration/java").exists()) {

    task integrationTest(type: Test) {
      description = 'Runs artifacts integration tests.'
      group = 'verification'

      useJUnitPlatform()

      environment "BUILD_DIR", buildDir
      environment "GIT_WRAPPER", "${projectDir}/src/integration/resources/git-wrapper.sh"

      testClassesDirs = sourceSets.integration.output.classesDirs
      classpath = sourceSets.integration.runtimeClasspath
      shouldRunAfter test


      minHeapSize = "512m"
      maxHeapSize = "${testJvmMaxMemory}"
      maxParallelForks = 4

      testlogger {
        theme 'standard'
        showStandardStreams true
        showPassedStandardStreams false
        showFailedStandardStreams true
        showPassed false
      }
    }
  }
  dependencies {
    implementation project(":clouddriver-artifacts")
  }
}

dependencies {
  implementation "io.spinnaker.kork:kork-web"
  implementation "com.squareup.okhttp3:okhttp"
  implementation "commons-codec:commons-codec"
  implementation "commons-io:commons-io"

  testImplementation "io.spinnaker.kork:kork-aws"
}
