dependencies {
  testImplementation "com.fasterxml.jackson.core:jackson-databind"
  testImplementation "com.mysql:mysql-connector-j"
  testImplementation "org.assertj:assertj-core"
  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testImplementation "org.postgresql:postgresql"
  testImplementation "org.slf4j:slf4j-api"
  testImplementation "org.testcontainers:junit-jupiter"
  testImplementation "org.testcontainers:mysql"
  testImplementation "org.testcontainers:postgresql"
  testImplementation "org.testcontainers:testcontainers"
  testRuntimeOnly "ch.qos.logback:logback-classic"
}

test.configure {
  def fullDockerImageName = System.getenv('FULL_DOCKER_IMAGE_NAME')
  onlyIf("there is a docker image to test") {
    fullDockerImageName != null && fullDockerImageName.trim() != ''
  }
}

test {
  // So stdout and stderr from the just-built container are available in CI
  testLogging.showStandardStreams = true

  // Run the tests when the docker image changes
  inputs.property 'fullDockerImageName', System.getenv('FULL_DOCKER_IMAGE_NAME')

  // Make the artifactTypes project property configured at build time (e.g. in
  // clouddriver/gradle.properties) available available as a system property so
  // tests (e.g. MySqlContainerTest) can enable them at run time.
  systemProperty "artifactTypes", project.findProperty("artifactTypes")
}
