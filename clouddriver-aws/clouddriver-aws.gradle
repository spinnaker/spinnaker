plugins {
  id 'com.adarshr.test-logger' version '2.1.0'
}

sourceSets {
  integration {
    java.srcDirs = ["src/integration/java"]
    resources.srcDirs = ["src/integration/resources"]
    compileClasspath += main.output + test.output
  }
}

configurations {
  integrationImplementation.extendsFrom testImplementation
  integrationRuntime.extendsFrom testRuntime
}

dependencies {
  implementation project(":cats:cats-core")
  implementation project(":clouddriver-api")
  implementation project(":clouddriver-configserver")
  implementation project(":clouddriver-core")
  implementation project(":clouddriver-eureka")
  implementation project(":clouddriver-security")
  implementation project(":clouddriver-saga")

  implementation "javax.inject:javax.inject:1"
  implementation "com.amazonaws:aws-java-sdk"
  implementation "com.github.ben-manes.caffeine:guava"
  implementation "com.netflix.awsobjectmapper:awsobjectmapper"
  implementation "com.netflix.frigga:frigga"
  implementation "io.spinnaker.fiat:fiat-api:$fiatVersion"
  implementation "io.spinnaker.fiat:fiat-core:$fiatVersion"
  implementation "io.spinnaker.kork:kork-cloud-config-server"
  implementation "io.spinnaker.kork:kork-core"
  implementation "io.spinnaker.kork:kork-aws"
  implementation "io.spinnaker.kork:kork-exceptions"
  implementation "io.spinnaker.kork:kork-secrets"
  implementation "io.spinnaker.kork:kork-security"
  implementation "io.spinnaker.kork:kork-credentials"
  implementation "io.spinnaker.kork:kork-moniker"
  implementation "io.spinnaker.kork:kork-retrofit"
  implementation "com.squareup.okhttp:okhttp"
  implementation "com.squareup.okhttp:okhttp-apache"
  implementation "com.squareup.okhttp:okhttp-urlconnection"
  implementation "com.squareup.retrofit:converter-jackson"
  implementation "com.squareup.retrofit:retrofit"
  implementation "io.reactivex:rxjava"
  implementation "org.apache.httpcomponents:httpclient"
  implementation "org.apache.httpcomponents:httpcore"
  implementation "org.codehaus.groovy:groovy"
  implementation "org.springframework.boot:spring-boot-actuator"
  implementation "org.springframework.boot:spring-boot-starter-web"
  implementation 'com.aestasit.infrastructure.sshoogr:sshoogr:0.9.25'
  implementation 'com.jcraft:jsch.agentproxy.connector-factory:0.0.9'
  implementation 'com.jcraft:jsch.agentproxy.jsch:0.0.9'
  implementation "com.github.wnameless.json:json-flattener:0.11.1"

  testImplementation "io.spinnaker.kork:kork-exceptions"
  testImplementation "cglib:cglib-nodep"
  testImplementation "com.natpryce:hamkrest"
  testImplementation "com.google.guava:guava"
  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testImplementation "org.objenesis:objenesis"
  testImplementation "org.spockframework:spock-core"
  testImplementation "org.spockframework:spock-spring"
  testImplementation "org.springframework.boot:spring-boot-starter-test"
  testImplementation "org.springframework:spring-test"

  integrationImplementation project(":clouddriver-web")
  integrationImplementation "org.springframework:spring-test"
  integrationImplementation sourceSets.test.output
  integrationImplementation sourceSets.main.output
  integrationImplementation ("io.rest-assured:rest-assured:4.0.0") {
    exclude group: "org.codehaus.groovy", module: "groovy" // use kork's version
  }
}

task integrationTest(type: Test) {
  description = "Runs AWS EC2 provider integration tests."
  group = 'verification'

  environment "PROJECT_ROOT", project.rootDir.toString()
  useJUnitPlatform()

  testClassesDirs = sourceSets.integration.output.classesDirs
  classpath = sourceSets.integration.runtimeClasspath
  shouldRunAfter test
  maxParallelForks = 4

  minHeapSize = "512m"
  maxHeapSize = "${testJvmMaxMemory}"

  testlogger {
    theme 'mocha'
    showFailedStandardStreams true
  }
}
