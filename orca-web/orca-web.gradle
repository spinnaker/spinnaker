/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.2.1.RELEASE"
    }
}

apply plugin: 'spring-boot'
apply plugin: 'spinnaker-debpublish'

springBoot {
    mainClass = 'com.netflix.spinnaker.orca.Main'
}

dependencies {
  spinnaker.group("bootWeb")

  compile spinnaker.dependency('redbatch')
  compile project(":orca-bakery")
  compile project(":orca-kato")
  compile project(":orca-oort")
  compile project(":orca-mort")
  compile project(":orca-front50")
  compile project(":orca-flex")
  compile project(":orca-echo")
  compile project(":orca-mine")
  compile project(":orca-rush")
  compile project(":orca-igor")
  compile project(":orca-eureka")

  runtime spinnaker.dependency("kork")
  compile spinnaker.dependency("korkWeb")

  spinnaker.group('test')
  testCompile spinnaker.dependency('batchTest')
  testCompile project(":orca-test")
}

applicationName = 'orca'
applicationDefaultJvmArgs = ["-Djava.security.egd=file:/dev/./urandom"]
applicationDistribution.from(project.file('config')) {
  into "config"
}

sourceSets {
  main {
    resources {
      srcDir 'src/main/resources'
      srcDir 'config'
    }
  }
}

jar {
  doFirst {
    exclude "${rootProject.name}.yml"
  }
}

startScripts {
  doLast {
    unixScript.text = unixScript.text.replace('CLASSPATH=$APP_HOME', 'CLASSPATH=$APP_HOME/config:$APP_HOME')
    windowsScript.text = windowsScript.text.replace('set CLASSPATH=', 'set CLASSPATH=%APP_HOME%\\config;')
  }
}

ospackage {
  packageName = project.applicationName
  version = project.version.replaceAll("-SNAPSHOT", "")
  release '3'
  into "/opt/${project.applicationName}"
  from "${project.buildDir}/install/${project.applicationName}"
}

buildDeb {
  dependsOn installApp
  requires("openjdk-7-jre")
  requires("redis-server")
}
