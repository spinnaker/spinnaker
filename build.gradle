buildscript {
  ext {
    springBootVersion = "1.2.8.RELEASE"
  }
  repositories {
    jcenter()
    maven { url "http://dl.bintray.com/spinnaker/gradle" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath 'com.netflix.spinnaker.gradle:spinnaker-gradle-project:3.12.0'
    classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
  }
}

group = "com.netflix.spinnaker.spinnaker"


apply plugin: 'spinnaker.project'
apply plugin: 'nebula.ospackage'
apply plugin: 'java'

String toVers(String v) {
  int idx = v.indexOf('-')
  if (idx != -1) {
    return v.substring(0, idx)
  }
  return v
}

ospackage {

// common installation paths 
  def spinnakerHome = "/opt/" + project.name

// global package requirements
  packageName = project.name
  version = toVers(project.version.toString())
  release '3'

// installer scripts
  preInstall = file('pkg_scripts/preInstall.sh')
  postInstall = file('pkg_scripts/postInstall.sh')
  postUninstall = file('pkg_scripts/postUninstall.sh')

// files to put in the meta package
  from('pylib') {
    into spinnakerHome + '/pylib'
  }

  from('cassandra') {
    into spinnakerHome + '/cassandra'
  }

  from('install') {
    into spinnakerHome + '/install'
  }

  from('runtime') {
    into spinnakerHome + '/bin'
    fileMode = 0755
  }  

  from(file('etc/init/spinnaker.conf')) {
    into('/etc/init')
    user = 'root'
    permissionGroup = 'root'
    fileType = CONFIG | NOREPLACE
  }

  from('config') {
    into spinnakerHome + '/config'
    fileMode = 0644
  }


  from('etc/apache2/sites-available/spinnaker.conf') {
    into '/etc/apache2/sites-available'
    user = 'root'
    permissionGroup = 'root'    
    fileMode = 0644
  }

  from('etc/default/spinnaker') {
    into '/etc/default'
    user = 'root'
    permissionGroup = 'root'    
    fileMode = 0644
  }

  configurationFile('/etc/default/spinnaker\n')
  configurationFile('/etc/apache2/sites-available/spinnaker.conf\n')

  File configDir
  FileCollection collection = files { configDir.listFiles() }

  configDir = file('config')
  collection.each { configurationFile(spinnakerHome + '/' + relativePath(it) + '\n') }
}


// Ubuntu
buildDeb {
  // requires('openjdk-8-jdk')
  // requires('oracle-java8-installer')
  requires('redis-server', '3.0.5', GREATER | EQUAL)
  requires('spinnaker-clouddriver')
  requires('spinnaker-deck')
  requires('spinnaker-echo')
  requires('spinnaker-fiat')
  requires('spinnaker-front50')
  requires('spinnaker-gate')
  requires('spinnaker-igor')
  requires('spinnaker-orca')
  requires('spinnaker-rosco')
  requires('apache2')
  
}


// TO DO 
buildRpm {
  arch = NOARCH
  os = LINUX
}

task publishInstallSpinnaker {
  onlyIf { project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey') }

  doLast {
    def url = "https://api.bintray.com/content/spinnaker/scripts/SpinnakerBootstrap/1.0/InstallSpinnaker.sh?publish=1&override=1".toURL()
    def conn = url.openConnection()

    conn.setRequestMethod('PUT')
    conn.setDoOutput(true)
    conn.addRequestProperty('Authorization', 'Basic ' + ("${project.property('bintrayUser')}:${project.property('bintrayKey')}".bytes.encodeBase64().toString()))
    conn.getOutputStream().withCloseable {
      it.write(project.file('InstallSpinnaker.sh').text.bytes)
      it.flush()
    }
    int code = conn.getResponseCode()

    if (code != 201) {
      logger.warn "Unexpected response code from InstallSpinnaker.sh publish: $code"
    } else {
      logger.info "Uploaded InstallSpinnaker.sh"
    }

    conn.disconnect()
  }
}

tasks.release.dependsOn('publishInstallSpinnaker')
