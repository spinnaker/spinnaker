/*
 * Copyright 2014 Netflix, Inc.
 * Copyright (c) 2018, salesforce.com, inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
buildscript {
  ext {
    kotlinVersion = "1.3.10"
    junitPlatformVersion = "1.0.2"
  }
  repositories {
    jcenter()
    maven { url "https://spinnaker.bintray.com/gradle" }
    maven { url "https://plugins.gradle.org/m2/" }
  }

  dependencies {
    classpath 'com.netflix.spinnaker.gradle:spinnaker-dev-plugin:5.2.1'
    classpath "org.junit.platform:junit-platform-gradle-plugin:${junitPlatformVersion}"
    classpath "com.netflix.nebula:nebula-kotlin-plugin:$kotlinVersion"
  }
}

allprojects {
  group = "com.netflix.spinnaker.clouddriver"
  apply plugin: 'spinnaker.base-project'
  apply plugin: 'groovy'

  ext {
    spinnakerDependenciesVersion = '1.44.0'
    if (project.hasProperty('spinnakerDependenciesVersion')) {
      spinnakerDependenciesVersion = project.property('spinnakerDependenciesVersion')
    }
  }

  def checkLocalVersions = [spinnakerDependenciesVersion: spinnakerDependenciesVersion]
  if (ext.has('versions')) {
    def extVers = ext.get('versions')
    if (extVers instanceof Map) {
      checkLocalVersions.putAll(extVers)
    }
  }

  def localVersions = checkLocalVersions.findAll { it.value.endsWith('-SNAPSHOT') }
  if (localVersions) {
    logger.info("Enabling mavenLocal repo for $localVersions")
    repositories {
      mavenLocal()
    }
  }

  spinnaker {
    dependenciesVersion = spinnakerDependenciesVersion
  }

  test {
    testLogging {
      exceptionFormat = 'full'
    }
    if (project.hasProperty('slowTest')) {
      long slow = 250
      try {
        slow = Long.parseLong(project.property('slowTest'))
      } catch (Exception ex) {
      }
      afterTest { desc, result ->
        long duration = result.getEndTime() - result.getStartTime()
        if (duration > slow) {
          logger.warn("test exceeded $slow ms: $desc.className :: $desc.name ($duration milliseconds)")
        }
      }
    }
    minHeapSize = "512m"
    maxHeapSize = "512m"
  }
}

subprojects { project ->

  configurations {
    all {
      exclude group: 'javax.servlet', module: 'servlet-api'
      exclude group: 'javax.ws.rs', module: 'jsr311-api'
      resolutionStrategy {
        force 'org.antlr:antlr-runtime:3.5.2'
        eachDependency {
          if (it.requested.group == 'asm' || it.requested.group == 'org.ow2.asm') {
            it.useTarget group: 'org.ow2.asm', name: 'asm-all', version: '5.0.3'
          }
          if (it.requested.group == 'junit') {
            it.useTarget group: 'junit', name: 'junit', version: '4.12'
          }
          if (it.requested.group == 'cglib' || it.requested.name == 'cglib') {
            it.useTarget group: 'cglib', name: 'cglib', version: '3.2.0'
          }
          if (it.requested.group == 'antlr') {
            it.useTarget group: 'org.antlr', name: it.requested.name, version: '3.5.2'
          }
          if (it.requested.group == 'org.apache.xbean') {
            it.useVersion '4.3'
          }
          if (it.requested.group == 'ch.qos.logback') {
            it.useVersion '1.2.3'
          }
        }
      }
    }
  }

  tasks.withType(JavaExec) {
    if (System.getProperty('DEBUG', 'false') == 'true') {
      jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=7102'
    }
  }

  dependencies {
    compile spinnaker.dependency('groovy')
    spinnaker.group('test')
  }

  //c&p this because NetflixOss reverts it to 1.7 and ends up getting applied last..
  project.plugins.withType(JavaBasePlugin) {
    JavaPluginConvention convention = project.convention.getPlugin(JavaPluginConvention)
    convention.sourceCompatibility = JavaVersion.VERSION_1_8
    convention.targetCompatibility = JavaVersion.VERSION_1_8
  }
}

defaultTasks ':clouddriver-web:run'
