/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
    maven { url "https://dl.bintray.com/spinnaker/gradle" }
    maven { url "https://plugins.gradle.org/m2/" }
  }
  dependencies {
    classpath 'com.netflix.spinnaker.gradle:spinnaker-dev-plugin:5.2.2'
  }
}

allprojects { project ->
  apply plugin: 'spinnaker.base-project'
  apply plugin: 'groovy'

  group = "com.netflix.spinnaker.front50"

  ext {
    spinnakerDependenciesVersion = '1.36.0'
    if (project.hasProperty('spinnakerDependenciesVersion')) {
      spinnakerDependenciesVersion = project.property('spinnakerDependenciesVersion')
    }
  }

  def checkLocalVersions = [spinnakerDependenciesVersion: spinnakerDependenciesVersion]
  if (ext.has('versions')) {
    def extVers = ext.get('versions')
    if (extVers instanceof Map) {
      checkLocalVersions.putAll(extVers)
    }
  }

  def localVersions = checkLocalVersions.findAll { it.value.endsWith('-SNAPSHOT') }
  if (localVersions) {
    logger.info("Enabling mavenLocal repo for $localVersions")
    repositories {
      mavenLocal()
    }
  }

  spinnaker {
    dependenciesVersion = spinnakerDependenciesVersion
  }

  test {
    testLogging {
      exceptionFormat = 'full'
    }
  }

  configurations.all {
    exclude group: 'org.mortbay.jetty', module: 'servlet-api'
    exclude group: 'javax.servlet', module: 'servlet-api'

    resolutionStrategy {
      force 'com.google.guava:guava:15.0'
      eachDependency {
        if (it.requested.group == "asm" || it.requested.group == "org.ow2.asm") {
          it.useTarget group: "org.ow2.asm", name: "asm-all", version: "5.0.4"
        }
        if (it.requested.group == "cglib" || it.requested.name == "cglib") {
          it.useTarget group: "cglib", name: "cglib", version: "3.1"
        }
        if (it.requested.group == "org.apache.xbean") {
          it.useVersion "4.4"
        }
      }
    }
  }

  tasks.withType(JavaExec) {
    if (System.getProperty('DEBUG', 'false') == 'true') {
      jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=8180'
    }
  }

  dependencies {
    compile spinnaker.dependency("groovy")
    spinnaker.group("test")
  }

  //c&p this because NetflixOss reverts it to 1.7 and ends up getting applied last..
  project.plugins.withType(JavaBasePlugin) {
    JavaPluginConvention convention = project.convention.getPlugin(JavaPluginConvention)
    convention.sourceCompatibility = JavaVersion.VERSION_1_8
    convention.targetCompatibility = JavaVersion.VERSION_1_8
  }
}

defaultTasks ':front50-web:run'
