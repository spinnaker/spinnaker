buildscript {
  repositories {
    jcenter()
    maven { url "http://dl.bintray.com/spinnaker/gradle" }
  }
  dependencies {
    classpath 'com.netflix.spinnaker:spinnaker-gradle-project:1.9.3'
  }
}

ext {
  springBootVersion = "1.0.2.RELEASE"
}

apply from: 'idea/ideaModule.gradle'

subprojects {
  apply plugin: "spinnaker-gradle-project"
  apply plugin: "idea"
  apply plugin: "groovy"

  group = "com.netflix.spinnaker.oort"

  configurations.all {
    resolutionStrategy {
      cacheChangingModulesFor 0, 'seconds'
      force 'com.google.guava:guava:14.0.1'
    }
    exclude group: 'javax.servlet', module: 'servlet-api'
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "org.mortbay.jetty", module: "servlet-api"
  }

  repositories {
    jcenter()
    maven { url "http://artifacts.netflix.com/libs-releases-local" }
    maven { url "http://repo.spring.io/repo" }
  }

  dependencies {
    compile "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
    compile 'org.codehaus.groovy:groovy-all:2.3.2'
    compile 'com.netflix.rxjava:rxjava-core:0.16.0'
    testCompile "org.spockframework:spock-core:0.7-groovy-2.0"
    testCompile "cglib:cglib-nodep:3.1"
  }

}


