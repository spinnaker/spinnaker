/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
    repositories {
        if (project.hasProperty('internalArtifactory')) {
            maven { url project.internalArtifactory }
        } else {
            jcenter()
        }
        maven { url 'http://dl.bintray.com/spinnaker/gradle/' }
    }
    dependencies {
        classpath "com.netflix.spinnaker:spinnaker-gradle-project:1.12.54"
    }
}

ext {
    versions = [
        batch   : "3.0.1.RELEASE",
        groovy  : "2.3.6",
        guava   : "14.0.1",
        jackson : "2.3.3",
        kork    : "1.23",
        okhttp  : "2.0.0",
        retrofit: "1.6.1",
        rx      : "0.20.2",
        slf4j   : "1.7.5",
        spock   : "1.0-groovy-2.3",
        spring  : "4.0.6.RELEASE"
    ]

    commonDependencies = [
        groovy         : dependencies.create("org.codehaus.groovy:groovy-all:$versions.groovy"),
        guava          : dependencies.create("com.google.guava:guava:$versions.guava"),
        hamcrest       : dependencies.create("org.hamcrest:hamcrest-all:1.3"),
        jacksonDatabind: dependencies.create("com.fasterxml.jackson.core:jackson-databind:$versions.jackson"),
        jacksonGuava   : dependencies.create("com.fasterxml.jackson.datatype:jackson-datatype-guava:$versions.jackson"),
        kork           : dependencies.create("com.netflix.spinnaker.kork:kork-core:$versions.kork"),
        korkJedis      : dependencies.create("com.netflix.spinnaker.kork:kork-jedis:$versions.kork"),
        korkCassandra  : dependencies.create("com.netflix.spinnaker.kork:kork-cassandra:$versions.kork"),
        logback        : dependencies.create("ch.qos.logback:logback-classic:$versions.logback"),
        okHttp         : dependencies.create("com.squareup.okhttp:okhttp-urlconnection:$versions.okhttp"),
        retrofit       : dependencies.create("com.squareup.retrofit:retrofit:$versions.retrofit"),
        rxJava         : dependencies.create("com.netflix.rxjava:rxjava-core:$versions.rx"),
        slf4j          : dependencies.create("org.slf4j:slf4j-api:$versions.slf4j"),
        slf4jJcl       : dependencies.create("org.slf4j:jcl-over-slf4j:$versions.slf4j"),
        slf4jSimple    : dependencies.create("org.slf4j:slf4j-simple:$versions.slf4j"),
        spock          : dependencies.create("org.spockframework:spock-core:$versions.spock"),
        spockSpring    : dependencies.create("org.spockframework:spock-spring:$versions.spock"),
        springBatch    : dependencies.create("org.springframework.batch:spring-batch-core:$versions.batch"),
        springBatchTest: dependencies.create("org.springframework.batch:spring-batch-test:$versions.batch"),
        springTest     : dependencies.create("org.springframework:spring-test:$versions.spring"),
        springWeb      : dependencies.create("org.springframework:spring-web:$versions.spring"),
        objenesis      : dependencies.create("org.objenesis:objenesis:2.1")
    ]
}

allprojects {
    apply plugin: "spinnaker-gradle-project"
    apply plugin: 'codenarc'
    apply plugin: 'jacoco'
    group = "com.netflix.spinnaker.echo"

    configurations.all {
        exclude group: 'javax.servlet', module: 'servlet-api'
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'

        resolutionStrategy {
            force 'org.apache.httpcomponents:httpclient:4.3.2'
        }
    }
}

subprojects {
    repositories {
        if (project.hasProperty('internalArtifactory')) {
            maven { url project.internalArtifactory }
        } else {
            jcenter()
        }
    }
    plugins.withType(JavaPlugin) {
        sourceCompatibility = "1.7"
        targetCompatibility = "1.7"
    }

    codenarc {
        toolVersion = '0.21'
        configFile = rootProject.file('config/codenarc/rules.groovy')
    }

    jacoco {
        toolVersion = '0.7.0.201403182114'
    }
}

defaultTasks "build"
tasks.checkSnapshotDependencies.enabled = false
