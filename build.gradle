/*
 * Copyright 2014 Netflix, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
    maven { url "http://dl.bintray.com/spinnaker/gradle" }
  }
  dependencies {
    classpath "com.netflix.spinnaker:spinnaker-gradle-project:1.12.82"
  }
}

apply from: "gradle/cglib-java8.gradle"

allprojects {
  apply plugin: "spinnaker-gradle-project"
  group = "com.netflix.spinnaker.orca"

  configurations.all {
    exclude group: "javax.servlet", module: "servlet-api"
    exclude group: "org.slf4j", module: "slf4j-log4j12"
    exclude group: "org.slf4j", module: "slf4j-simple"
    resolutionStrategy {
      eachDependency {
        if (it.requested.group.startsWith("com.fasterxml.jackson")) {
          it.useVersion "2.4.4"
        }
        if (it.requested.group == "com.netflix.rxjava" && it.requested.name == "rxjava-core") {
          it.useTarget group: "io.reactivex", name: "rxjava", version: "1.0.5"
        }
        if (it.requested.group == "org.springframework.batch") {
          it.useVersion "3.0.3.RELEASE"
        }
      }
    }
  }
}
subprojects {
  apply plugin: 'groovy'
  plugins.withType(JavaPlugin) {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
  }
  dependencies {
    spinnaker.group('spockBase')
    compile spinnaker.dependency('groovy')
    compile spinnaker.dependency('guava')
    compile spinnaker.dependency('slf4jApi')
    testRuntime spinnaker.dependency('slf4jSimple')
  }
}

tasks.checkSnapshotDependencies.enabled = false
defaultTasks ":orca-web:bootRun"
