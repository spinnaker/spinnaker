/*
 * Copyright 2014 Netflix, Inc.
 * Copyright (c) 2018, salesforce.com, inc. All rights reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  ext {
    junitPlatformVersion = "1.0.2"
  }

  repositories {
    gradlePluginPortal()
  }

  dependencies {
    classpath "org.junit.platform:junit-platform-gradle-plugin:${junitPlatformVersion}"
  }
}

plugins {
  id 'io.spinnaker.project' version "$spinnakerGradleVersion" apply false
  id 'org.jetbrains.kotlin.jvm' version "$kotlinVersion"
  id 'org.jetbrains.kotlin.plugin.allopen' version "$kotlinVersion" apply false
  id "com.google.protobuf" version "0.8.12" apply false
}

allprojects {
  apply plugin: 'io.spinnaker.project'
}

subprojects {
  group = "io.spinnaker.clouddriver"

  if (name != "clouddriver-bom" && name != "clouddriver-api") {
    apply plugin: 'java-library'
    apply plugin: 'groovy'
    apply plugin: 'kotlin'
    apply plugin: "kotlin-allopen"
    apply plugin: "jacoco"

    sourceSets.main.java.srcDirs = []
    sourceSets.main.groovy.srcDirs += ["src/main/java"]

    dependencies {
      api enforcedPlatform("io.spinnaker.kork:kork-bom:$korkVersion")

      compileOnly enforcedPlatform("io.spinnaker.kork:kork-bom:$korkVersion")
      compileOnly "org.projectlombok:lombok"

      annotationProcessor enforcedPlatform("io.spinnaker.kork:kork-bom:$korkVersion")
      annotationProcessor "org.projectlombok:lombok"
      annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")

      testAnnotationProcessor enforcedPlatform("io.spinnaker.kork:kork-bom:$korkVersion")
      testAnnotationProcessor "org.projectlombok:lombok"

      testCompileOnly enforcedPlatform("io.spinnaker.kork:kork-bom:$korkVersion")
      testCompileOnly "org.projectlombok:lombok"

      testRuntimeOnly enforcedPlatform("io.spinnaker.kork:kork-bom:$korkVersion")
      testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
    }

    test {
      useJUnitPlatform()
      testLogging {
        exceptionFormat = 'full'
        if (project.hasProperty('slowTest')) {
          long slow = 250
          try {
            slow = Long.parseLong(project.property('slowTest'))
          } catch (Exception ex) {
          }
          afterTest { desc, result ->
            long duration = result.getEndTime() - result.getStartTime()
            if (duration > slow) {
              logger.warn("test exceeded $slow ms: $desc.className :: $desc.name ($duration milliseconds)")
            }
          }
        }

      }
      minHeapSize = "512m"
      maxHeapSize = "1g"
      maxParallelForks = 4
      jacoco {
        enabled = project.hasProperty('testCoverage')
      }
    }

    // The test report requires tests to have run first
    jacocoTestReport {
      dependsOn test
    }
  }

  if ([korkVersion, fiatVersion].any { it.endsWith("-SNAPSHOT") }) {
    logger.info("Enabling mavenLocal")
    repositories {
      mavenLocal()
    }
  }

  tasks.withType(JavaCompile).configureEach {
    javaCompiler = javaToolchains.compilerFor {
      languageVersion = JavaLanguageVersion.of(11)
    }
  }
  tasks.withType(Test).configureEach {
    javaLauncher = javaToolchains.launcherFor {
      languageVersion = JavaLanguageVersion.of(17)
    }
  }

  tasks.withType(JavaExec) {
    if (System.getProperty('DEBUG', 'false') == 'true') {
      jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=7102'
    }
  }
}

defaultTasks ':clouddriver-web:run'
